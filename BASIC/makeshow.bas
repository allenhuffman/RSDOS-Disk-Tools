10 'MAKESHOW.BAS
20 CLEAR 1000:DIM SP$(1)

30 FOR G=0 TO 67
40 F$=MID$(STR$(G),2)
50 PRINT #-2,F$".TXT: ";
60 OPEN "O",#1,F$+".TXT"
70 T$=STRING$(128,G)
80 FOR I=1 TO 9
90 PRINT #1,T$;
100 NEXT
110 CLOSE #1
120 GOSUB 150
130 NEXT
140 END

150 ' S - SECTOR NUMBER
160 FOR S=3 TO 11
170 ' SP$(0-1) - SECTOR PARTS
180 DSKI$ 0,17,S,SP$(0),SP$(1)
190 ' P - PART OF SECTOR
200 FOR P=0 TO 1
210 ' E - DIR ENTRY (4 P/SECT.)
220 FOR E=0 TO 3
230 ' GET 32 BYTE DIR ENTRY
240 DE$=MID$(SP$(P),1+E*32,32)
250 ' FB - FIRST BYTE OF NAME
260 FB=ASC(LEFT$(DE$,1))
270 ' SKIP DELETED FILES
280 IF FB=0 THEN 330
290 ' WHEN 255, DIR IS DONE
300 IF FB=255 THEN 360
310 ' MATCH NAME
320 IF F$=LEFT$(DE$,LEN(F$)) THEN PRINT#-2,"AT GRANULE";ASC(MID$(DE$,14,1)):GOTO 360
330 NEXT
340 NEXT
350 NEXT
360 RETURN
